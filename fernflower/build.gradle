
apply plugin: 'java'

task downloadAndExtractFernflower {

    doLast {

        File fernflowerZip = file("${buildDir}/fernflower.zip")

        if (!fernflowerZip.file) {
            fernflowerZip.parentFile.mkdirs()
            new URL('https://github.com/fesh0r/fernflower/archive/master.zip').withInputStream { input ->
                fernflowerZip.withOutputStream { output ->
                    output << input
                }
            }
        }

        copy {
            from zipTree(fernflowerZip)
            into projectDir
        }

   }

}

repositories {

    jcenter()

}

dependencies {

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'org.assertj:assertj-core:3.6.1'

}

sourceSets {

    main {
        java {
            srcDirs = ['fernflower-master/src']
        }
        resources {
            srcDirs = []
        }
    }

    test {
        java {
            srcDirs = ['fernflower-master/test']
        }
        resources {
            srcDirs = []
        }
    }

}

test.onlyIf { false }
